<PLAYER_SETUP>
random_placement
ai_info_map_type BLACK_FOREST 0 0 0

<LAND_GENERATION>

base_terrain GRASS

#const FOREST2 20


create_player_lands
{
  terrain_type	                GRASS2
  left_border                   5
  right_border                  5
  top_border                    5
  bottom_border                 5
  /*base_size                     11 */
  number_of_tiles               100
  other_zone_avoidance_distance 15
}

<ELEVATION_GENERATION>

create_elevation 6 {
  number_of_clumps 128
  number_of_tiles 256
  set_scale_by_size
}


<TERRAIN_GENERATION>

create_terrain FOREST
{
	base_terrain GRASS
	land_percent 50
	number_of_clumps 500
	set_avoid_player_start_areas
	spacing_to_other_terrain_types 2
}

create_terrain FOREST2
{
	base_terrain GRASS
	land_percent 50
	number_of_clumps 500
	set_avoid_player_start_areas
	spacing_to_other_terrain_types 3
}

<OBJECTS_GENERATION>

create_object TOWN_CENTER 
{ 
     set_place_for_every_player 
     min_distance_to_players 0 
     max_distance_to_players 0 
} 

if EMPIRE_WARS
  create_object VILLAGER
  {
    number_of_objects 27
    set_place_for_every_player
    min_distance_to_players 3 
    max_distance_to_players 6 
  }
else
  create_object VILLAGER 
  { 
    set_place_for_every_player 
    min_distance_to_players 3 
    max_distance_to_players 6 
  }
endif

create_object SCOUT /* This will create either a Scout Cavalry or an Eagle Warrior. */ 
{ 
     number_of_objects 1 
     set_place_for_every_player 
     min_distance_to_players 7 
     max_distance_to_players 9 
} 

create_object FORAGE 
{ 
     number_of_objects 6 
     group_placement_radius 3 
     set_tight_grouping 
     set_gaia_object_only 
     set_place_for_every_player 
     min_distance_to_players 8 
     max_distance_to_players 10 
     min_distance_group_placement 4 
} 

create_object GOLD
{
  number_of_objects          4
  group_placement_radius     3
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    7
  max_distance_to_players    10
  temp_min_distance_group_placement  5
}

create_object STONE
{
  number_of_objects          4
  group_placement_radius     3
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    9
  max_distance_to_players    12
  temp_min_distance_group_placement  5
}

create_object GOLD
{
  number_of_groups           2
  number_of_objects          2
  group_placement_radius     3
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    18
  max_distance_to_players    28
  temp_min_distance_group_placement  5
}

create_object STONE
{
  number_of_groups           2
  number_of_objects          2
  group_placement_radius     3
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    18
  max_distance_to_players    28
  temp_min_distance_group_placement  5
}

create_object RELIC
{
   number_of_objects            5
   min_distance_to_players     25
   temp_min_distance_group_placement 50
}

create_object DEER
{
  number_of_objects          5
  number_of_groups			1
  set_gaia_object_only
  set_place_for_every_player
  terrain_to_place_on 		GRASS
  temp_min_distance_group_placement 2
  min_distance_to_players		5
  max_distance_to_players		12
}

create_object BOAR
{
  number_of_objects          1
  number_of_groups			2
  set_gaia_object_only
  set_place_for_every_player
  terrain_to_place_on 		GRASS
  temp_min_distance_group_placement 2
  min_distance_to_players		5
  max_distance_to_players		15
}

create_object SHEEP
{
  number_of_groups			1
  number_of_objects			4
  min_distance_to_players		7
  max_distance_to_players		9
  set_gaia_object_only
  set_place_for_every_player
  min_distance_group_placement		4
}

create_object SHEEP
{
  number_of_groups			2
  number_of_objects			2
  min_distance_to_players		16
  max_distance_to_players		20
  set_gaia_object_only
  set_place_for_every_player
  min_distance_group_placement		4
}

<CONNECTION_GENERATION>

create_connect_teams_lands {
    replace_terrain JUNGLE GRASS
    replace_terrain FOREST GRASS
    terrain_size 1 1
    terrain_cost 15
}